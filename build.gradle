buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.0.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
    }
}



apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'asset-pipeline'

group = 'com.morpheusdata.vault'
sourceCompatibility = '1.11'
targetCompatibility = '1.11'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	provided
}

dependencies {
	provided "com.morpheusdata:morpheus-plugin-api:0.13.1-SNAPSHOT"
	provided "org.codehaus.groovy:groovy-all:$groovyVersion"
	implementation 'commons-beanutils:commons-beanutils:1.9.3'
	implementation "org.slf4j:slf4j-api:$slf4jVersion"
	implementation "org.slf4j:slf4j-parent:$slf4jVersion"
	implementation 'commons-net:commons-net:3.6'

	// Include morpheus-core and it's dependencies
	testImplementation 'io.reactivex.rxjava2:rxjava:2.2.0'
	testImplementation 'org.apache.httpcomponents:httpclient:4.5.3'
	testImplementation 'org.apache.httpcomponents:httpcore:4.4.5'
	testImplementation "org.slf4j:slf4j-parent:$slf4jVersion"

	testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	testImplementation 'net.bytebuddy:byte-buddy:1.9.3'
	testImplementation 'org.objenesis:objenesis:2.6'
	testImplementation "org.spockframework:spock-core:$spockVersion"
	testImplementation 'cglib:cglib-nodep:3.2.12'
}


sourceSets {
	main {
		compileClasspath += configurations.provided
	}
}

jar {
	manifest {
		attributes(
				'Plugin-Class': 'com.morpheusdata.vault.VaultPlugin',
				'Plugin-Version': version,
				'Morpheus-Name': 'Hashicorp Vault',
				'Morpheus-Organization': 'morpheus',
				'Morpheus-Code': 'vault-plugin',
				'Morpheus-Description': 'Plugin for Vault Credential Storage',
				'Morpheus-Logo': 'assets/efficientIP140-black.svg',
				'Morpheus-Logo-Dark': 'assets/efficientIP140-white.svg',
				'Morpheus-Color': '#ffffff',
				'Morpheus-Labels': 'Plugin, Credentials, Vault',
				'Morpheus-Repo': 'https://github.com/gomorpheus/morpheus-plugin-core',
				'Morpheus-Min-Appliance-Version': "5.5.0"
		)
	}
}


task(console, dependsOn: 'classes', type: JavaExec) {
	main = 'groovy.ui.Console'
	classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

shadowJar {
	// archiveFileName = "${baseName}-${version}.mpl"
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}


tasks.assemble.dependsOn tasks.shadowJar
